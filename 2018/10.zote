include!("stdlib");
include!("aoc.zote");

input := read("input") >> trim;
lines := input >> split("\n") >> map(ints);
found := false;

for t in [0:1000000] {
    for line in lines {
        line[0] += line[2];
        line[1] += line[3];
    }
    maxr := lines >> map(\>> [1]) >> maximum;
    minr := lines >> map(\>> [1]) >> minimum;

    if maxr - minr < 15 {
        found = true;
        points := lines >> map(\>> [:2]) >> set;
    
        maxc := lines >> map(\>> [0]) >> maximum;
        minc := lines >> map(\>> [0]) >> minimum;

        for r in [minr:maxr+1] {
            row := "";
            for c in [minc:maxc+1] if [c, r] >> in(points) "#" >> push(row) else "." >> push(row);
            row >> print;
        }
        print(t+1);
    } else if found break;
}
